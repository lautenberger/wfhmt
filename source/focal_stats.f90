! *****************************************************************************
PROGRAM FOCAL_STATS
! *****************************************************************************

USE VARS
USE SUBS
USE IO

IMPLICIT NONE

INTEGER :: I, N, IOS, IROW, ICOL, IX, IY, ICOUNT, ICOUNT_TOTAL, ICOUNT_FOUND, IBAND
INTEGER, DIMENSION(1:100000) :: IXLO,IXHI,IYLO,IYHI
INTEGER :: VAL
REAL, ALLOCATABLE, DIMENSION(:) :: Z
CHARACTER(400) :: FN, NAMELIST_FN
INTEGER, DIMENSION(1:NMAX) :: ILOC
TYPE (RASTER_TYPE) :: RASTER_ONE, RASTER_TWO
REAL :: DISTANCE, RADIUS, DX, DY, WEIGHT
REAL, ALLOCATABLE, DIMENSION (:,:) :: DISTANCE_LOOKUP_TABLE
DOUBLE PRECISION :: SUM
LOGICAL :: BOX = .FALSE., CALC_AVERAGE_WITHIN_RADIUS = .FALSE., USE_INVERSE_WEIGHTING=.FALSE., SUMONLY=.FALSE.

NAMELIST /FOCAL_STATS_INPUTS/ INPUT_DIRECTORY, OUTPUT_DIRECTORY, FOCAL_STATS_INPUT_FILENAME, FOCAL_STATS_OUTPUT_FILENAME, &
                              A_SRS, COMPRESS, PATH_TO_GDAL, SCRATCH, RADIUS, VAL, BOX, CALC_AVERAGE_WITHIN_RADIUS, &
                              USE_INVERSE_WEIGHTING, SUMONLY

!Begin by getting input file name:
CALL GETARG(1,NAMELIST_FN)
IF (NAMELIST_FN(1:1)==' ') THEN
   WRITE(*,*) "Error, no input file specified."
   WRITE(*,*) "Hit Enter to continue."
   READ(5,*)
   STOP
ENDIF

!Now read NAMELIST group:
WRITE(*,*) 'Reading &FOCAL_STATS_INPUTS namelist group from ', TRIM(NAMELIST_FN)

! Set defaults:
CALL SET_NAMELIST_DEFAULTS
A_SRS='null'

! Open input file and read in namelist group
OPEN(LUINPUT,FILE=TRIM(NAMELIST_FN),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS)
IF (IOS .GT. 0) THEN
   WRITE(*,*) 'Problem opening input file ', TRIM(NAMELIST_FN)
   STOP
ENDIF

READ(LUINPUT,NML=FOCAL_STATS_INPUTS,END=100,IOSTAT=IOS)
 100  IF (IOS > 0) THEN
         WRITE(*,*) 'Error: Problem with namelist group &FOCAL_STATS_INPUTS.'
         STOP
      ENDIF
CLOSE(LUINPUT)

!Get operating system (linux or windows/dos)
CALL GET_OPERATING_SYSTEM

!Get coordinate system string
IF ( TRIM(A_SRS) .EQ. 'null' ) THEN
   FN = TRIM(INPUT_DIRECTORY) // TRIM(FOCAL_STATS_INPUT_FILENAME)
   CALL GET_COORDINATE_SYSTEM(FN)
ENDIF

!CALL READ_BSQ_RASTER_NEW (RASTER_ONE, INPUT_DIRECTORY, FOCAL_STATS_INPUT_FILENAME)
WRITE (*,*) 'Reading ', TRIM(FOCAL_STATS_INPUT_FILENAME)
FN = TRIM(INPUT_DIRECTORY) // TRIM(FOCAL_STATS_INPUT_FILENAME)
CALL READ_BIL_RASTER (RASTER_ONE, FN)

! Allocate empty raster
CALL ALLOCATE_EMPTY_RASTER(RASTER_TWO ,RASTER_ONE%NCOLS,RASTER_ONE%NROWS,RASTER_ONE%NBANDS,RASTER_ONE%XLLCORNER,RASTER_ONE%YLLCORNER,RASTER_ONE%XDIM,RASTER_ONE%YDIM,RASTER_ONE%NODATA_VALUE,1,'FLOAT     ')

DO ICOL = 1, RASTER_ONE%NCOLS
   IXLO(ICOL) = MAX(1               , ICOL - NINT(RADIUS/RASTER_ONE%XDIM))
   IXHI(ICOL) = MIN(RASTER_ONE%NCOLS, ICOL + NINT(RADIUS/RASTER_ONE%XDIM))
ENDDO

DO IROW = 1, RASTER_ONE%NROWS
   IYLO(IROW) = MAX(1               , IROW - NINT(RADIUS/RASTER_ONE%YDIM))
   IYHI(IROW) = MIN(RASTER_ONE%NROWS, IROW + NINT(RADIUS/RASTER_ONE%YDIM))
ENDDO

WEIGHT=1E0

IF (CALC_AVERAGE_WITHIN_RADIUS) THEN

   N = NINT(RADIUS/RASTER_ONE%XDIM) + 2
   ALLOCATE(DISTANCE_LOOKUP_TABLE(0:N,0:N)); DISTANCE_LOOKUP_TABLE(:,:) = 0D0

   DO IX = 0, N
   DO IY = 0, N
      DX = REAL (IX) * RASTER_ONE%XDIM
      DY = REAL (IY) * RASTER_ONE%YDIM
      DISTANCE_LOOKUP_TABLE(IX, IY) = SQRT(DX*DX + DY*DY)
   ENDDO
   ENDDO

   DO IBAND = 1, RASTER_ONE%NBANDS
!$omp PARALLEL DO DEFAULT(NONE) SCHEDULE(STATIC) COLLAPSE(2) &
!$omp PRIVATE(IBAND,IROW,ICOL,IX,IY,SUM,ICOUNT,DISTANCE,WEIGHT) &
!$omp SHARED(RASTER_ONE,RASTER_TWO,IXLO,IXHI,IYLO,IYHI,DISTANCE_LOOKUP_TABLE,USE_INVERSE_WEIGHTING,RADIUS,SUMONLY)
      DO IROW = 1, RASTER_ONE%NROWS
      DO ICOL = 1, RASTER_ONE%NCOLS
         SUM    = 0D0
         ICOUNT = 0
         DO IX = IXLO(ICOL), IXHI(ICOL)
         DO IY = IYLO(IROW), IYHI(IROW)
            DISTANCE = DISTANCE_LOOKUP_TABLE( ABS(IX-ICOL), ABS(IY-IROW) )
            IF (USE_INVERSE_WEIGHTING) WEIGHT = MIN( MAX( 0., 1.0 - (DISTANCE / RADIUS)), 1.0)
            IF ( (DISTANCE .LE. RADIUS) .AND. ABS(RASTER_ONE%RZT(IBAND,IX,IY) - RASTER_ONE%NODATA_VALUE) .GT. 0.01 ) THEN
               SUM = SUM + WEIGHT * DBLE(RASTER_ONE%RZT(IBAND,IX,IY))
               ICOUNT = ICOUNT + 1
            ENDIF
         ENDDO
         ENDDO
         IF (SUMONLY) THEN
            RASTER_TWO%RZT(IBAND,ICOL,IROW) = REAL(SUM)
         ELSE
            RASTER_TWO%RZT(IBAND,ICOL,IROW) = REAL(SUM) / REAL(ICOUNT)
         ENDIF
      ENDDO
      ENDDO
!$omp END PARALLEL DO
   ENDDO
ELSE
   IF (BOX) THEN
!$omp PARALLEL DO DEFAULT(NONE) SCHEDULE(STATIC) COLLAPSE(2) &
!$omp PRIVATE(IROW,ICOL,IX,IY,ICOUNT_TOTAL,ICOUNT_FOUND,DX,DY,DISTANCE) &
!$omp SHARED(RASTER_ONE,RASTER_TWO,IXLO,IXHI,IYLO,IYHI,VAL)
      DO IROW = 1, RASTER_ONE%NROWS
      DO ICOL = 1, RASTER_ONE%NCOLS
         IF (ICOL .EQ. 1 .AND. MOD(IROW,10) .EQ. 0) WRITE(*,*) IROW
         ICOUNT_TOTAL = 0
         ICOUNT_FOUND = 0      
         DO IX = IXLO(ICOL), IXHI(ICOL)
         DO IY = IYLO(IROW), IYHI(IROW)
               ICOUNT_TOTAL = ICOUNT_TOTAL + 1
               IF (RASTER_ONE%I2ZT(1,IX,IY) .EQ. VAL) ICOUNT_FOUND = ICOUNT_FOUND + 1
         ENDDO
         ENDDO
         RASTER_TWO%RZT(1,ICOL,IROW) = 100.0 * REAL(ICOUNT_FOUND) / REAL(ICOUNT_TOTAL)
      ENDDO
      ENDDO
!$omp END PARALLEL DO
   ELSE
!$omp PARALLEL DO DEFAULT(NONE) SCHEDULE(STATIC) COLLAPSE(2) &
!$omp PRIVATE(IROW,ICOL,IX,IY,ICOUNT_TOTAL,ICOUNT_FOUND,DX,DY,DISTANCE) &
!$omp SHARED(RASTER_ONE,RASTER_TWO,IXLO,IXHI,IYLO,IYHI,VAL,RADIUS)
      DO IROW = 1, RASTER_ONE%NROWS
      DO ICOL = 1, RASTER_ONE%NCOLS
         IF (ICOL .EQ. 1 .AND. MOD(IROW,10) .EQ. 0) WRITE(*,*) IROW
         ICOUNT_TOTAL = 0
         ICOUNT_FOUND = 0
         DO IX = IXLO(ICOL), IXHI(ICOL)
         DO IY = IYLO(IROW), IYHI(IROW)
            DX   = ABS(IX - ICOL) * RASTER_ONE%XDIM
            DY   = ABS(IY - IROW) * RASTER_ONE%YDIM
            DISTANCE = SQRT(DX*DX + DY*DY)
            IF (DISTANCE .LE. RADIUS) THEN
               ICOUNT_TOTAL = ICOUNT_TOTAL + 1
               IF (RASTER_ONE%I2ZT(1,IX,IY) .EQ. VAL) ICOUNT_FOUND = ICOUNT_FOUND + 1
            ENDIF
         ENDDO
         ENDDO
         RASTER_TWO%RZT(1,ICOL,IROW) = 100.0 * REAL(ICOUNT_FOUND) / REAL(ICOUNT_TOTAL)
      ENDDO
      ENDDO
!$omp END PARALLEL DO
   ENDIF

ENDIF

WRITE (*,*) 'Writing ', TRIM(FOCAL_STATS_OUTPUT_FILENAME)

CALL WRITE_BIL_RASTER_NEW(RASTER_TWO,OUTPUT_DIRECTORY,FOCAL_STATS_OUTPUT_FILENAME,.TRUE.,COMPRESS)

STOP

! *****************************************************************************
END PROGRAM FOCAL_STATS
! *****************************************************************************
