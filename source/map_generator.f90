! *****************************************************************************
PROGRAM MAP_GENERATOR
! *****************************************************************************
! This program calculates a wildland fire hazard map based on a user-specified 
! recipe

USE VARS
USE IO
USE SUBS

IMPLICIT NONE

INTEGER :: I, J, K, IOS, IROW, ICOL, ICOUNT, ICOMP, ICAT, ITHRESH
LOGICAL :: NODATA
CHARACTER(400) :: FN, NAMELIST_FN
REAL :: VAL

NAMELIST /MAP_GENERATOR_INPUTS/ INPUT_DIRECTORY_FOR_COMPONENT, OUTPUT_DIRECTORY, A_SRS, &
                                NUMBER_OF_COMPONENTS, NUMBER_OF_CATEGORIES_FOR_COMPONENT, CATEGORY_THRESHOLDS_FOR_COMPONENT, &
                                NUMBER_OF_CATEGORIES_FOR_FINAL_HAZARD_SCALE, CATEGORY_THRESHOLDS_FOR_FINAL_HAZARD_SCALE, &
                                OUTPUT_FILENAME_OF_CATEGORIZED_COMPONENT, OUTPUT_FILENAME_OF_FINAL_HAZARD_SCALE, & 
                                WEIGHTING_FACTOR_FOR_COMPONENT, THRESHOLDS_FOR_FINAL_HAZARD_RANKING, DISCRETE_OR_CONTINUOUS, &
                                ADDITIVE_OR_MULTIPLICATIVE, FILENAME_OF_COMPONENT, CATEGORY_SCORES_FOR_COMPONENT, &
                                CATEGORY_SCORES_FOR_FINAL_HAZARD_SCALE, USE_3BY3_SCORING, COLOR_TABLE_FOR_COMPONENT, &
                                COLOR_TABLE_FOR_FINAL_HAZARD_SCALE, PATH_TO_GDAL, SCRATCH

!Begin by getting input file name:
CALL GETARG(1,NAMELIST_FN)
IF (NAMELIST_FN(1:1)==' ') THEN
   WRITE(*,*) "Error, no input file specified."
   WRITE(*,*) "Hit Enter to continue."
   READ(5,*)
   STOP
ENDIF

!Now read NAMELIST group:
WRITE(*,*) 'Reading &MAP_GENERATOR_INPUTS namelist group from ', TRIM(NAMELIST_FN)

! Set defaults:
CALL SET_NAMELIST_DEFAULTS

! Open input file and read in namelist group
OPEN(LUINPUT,FILE=TRIM(NAMELIST_FN),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS)
IF (IOS .GT. 0) THEN
   WRITE(*,*) 'Problem opening input file ', TRIM(NAMELIST_FN)
   STOP
ENDIF

READ(LUINPUT,NML=MAP_GENERATOR_INPUTS,END=100,IOSTAT=IOS)
 100  IF (IOS > 0) THEN
         WRITE(*,*) 'Error: Problem with namelist group &MAP_GENERATOR_INPUTS.'
         STOP
      ENDIF
CLOSE(LUINPUT)

!Get operating system (linux or windows/dos)
CALL GET_OPERATING_SYSTEM

! Check for errors
CALL MAP_GENERATOR_ERROR_CHECKING

! Get coordinate system string
FN=TRIM(INPUT_DIRECTORY_FOR_COMPONENT(1)) // TRIM(FILENAME_OF_COMPONENT(1))
CALL GET_COORDINATE_SYSTEM(FN)

! Read in tiles of all input components:
DO ICOMP = 1, NUMBER_OF_COMPONENTS
   CALL READ_BSQ_RASTER(INPUTCOMP(ICOMP), INPUT_DIRECTORY_FOR_COMPONENT(ICOMP), FILENAME_OF_COMPONENT(ICOMP))

! Allocate rasters for output components and final hazard map
   CALL ALLOCATE_EMPTY_RASTER(OUTPUTCOMP(ICOMP),INPUTCOMP(1)%NCOLS,INPUTCOMP(1)%NROWS,1,INPUTCOMP(1)%XLLCORNER,INPUTCOMP(1)%YLLCORNER,INPUTCOMP(1)%XDIM,INPUTCOMP(1)%YDIM,INPUTCOMP(1)%NODATA_VALUE,1,'FLOAT     ')

! Now calculate each component:
   DO IROW = 1, INPUTCOMP(1)%NROWS
   DO ICOL = 1, INPUTCOMP(1)%NCOLS
      OUTPUTCOMP(ICOMP)%RZT(1,ICOL,IROW) = INPUTCOMP(ICOMP)%NODATA_VALUE
      IF (ABS(INPUTCOMP(ICOMP)%RZT(1,ICOL,IROW) - INPUTCOMP(ICOMP)%NODATA_VALUE) .LT. 0.1) THEN 
          OUTPUTCOMP(ICOMP)%RZT(1,ICOL,IROW) = 0.
          INPUTCOMP(ICOMP)%RZT (1,ICOL,IROW) = 0.             
      ELSE

! Example of scoring:
! Thresholds:         25     50     75          
! Score:           1      2      3      4
         SELECT CASE (TRIM(DISCRETE_OR_CONTINUOUS))
            CASE ('DISCRETE')
               OUTPUTCOMP(ICOMP)%RZT(1,ICOL,IROW) = CATEGORY_SCORES_FOR_COMPONENT(ICOMP,1)
               DO ITHRESH = 1, NUMBER_OF_CATEGORIES_FOR_COMPONENT(ICOMP)-1
                  ICAT = ITHRESH + 1
                  IF (INPUTCOMP(ICOMP)%RZT(1,ICOL,IROW) .GT. CATEGORY_THRESHOLDS_FOR_COMPONENT(ICOMP,ITHRESH)) OUTPUTCOMP(ICOMP)%RZT(1,ICOL,IROW) = CATEGORY_SCORES_FOR_COMPONENT(ICOMP,ICAT)
               ENDDO
            CASE ('CONTINUOUS')
               OUTPUTCOMP(ICOMP)%RZT(1,ICOL,IROW) = INPUTCOMP(ICOMP)%RZT(1,ICOL,IROW)
         END SELECT
      ENDIF
   ENDDO !ICOL
   ENDDO !IROW

! Now write newly-calculated tile to disk:
   CALL WRITE_BIL_RASTER(OUTPUTCOMP(ICOMP), OUTPUT_DIRECTORY, OUTPUT_FILENAME_OF_CATEGORIZED_COMPONENT(ICOMP), .TRUE., .TRUE.)

ENDDO !ICOMP

! Calculate map
CALL ALLOCATE_EMPTY_RASTER(OUTPUTCOMP(0),OUTPUTCOMP(1)%NCOLS,OUTPUTCOMP(1)%NROWS,1,OUTPUTCOMP(1)%XLLCORNER,OUTPUTCOMP(1)%YLLCORNER,OUTPUTCOMP(1)%XDIM,OUTPUTCOMP(1)%YDIM,OUTPUTCOMP(1)%NODATA_VALUE,1,'FLOAT     ')

DO IROW = 1, OUTPUTCOMP(1)%NROWS
DO ICOL = 1, OUTPUTCOMP(1)%NCOLS   
   OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = OUTPUTCOMP(0)%NODATA_VALUE
   NODATA = .FALSE. 
   DO ICOMP = 1, NUMBER_OF_COMPONENTS
      IF (ABS(INPUTCOMP(ICOMP)%RZT(1,ICOL,IROW) - INPUTCOMP(ICOMP)%NODATA_VALUE) .LT. 0.1) NODATA = .TRUE. 
   ENDDO
   IF (NODATA) THEN
      OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = 0.
   ELSE
      SELECT CASE (TRIM(ADDITIVE_OR_MULTIPLICATIVE))
         CASE ('ADDITIVE')
            OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = 0.
            DO ICOMP = 1, NUMBER_OF_COMPONENTS
               OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = OUTPUTCOMP(0)%RZT(1,ICOL,IROW) + WEIGHTING_FACTOR_FOR_COMPONENT(ICOMP)*OUTPUTCOMP(ICOMP)%RZT(1,ICOL,IROW)
            ENDDO
         CASE ('MULTIPLICATIVE')
            OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = 1.
            DO ICOMP = 1, NUMBER_OF_COMPONENTS
               OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = OUTPUTCOMP(0)%RZT(1,ICOL,IROW) * WEIGHTING_FACTOR_FOR_COMPONENT(ICOMP)*OUTPUTCOMP(ICOMP)%RZT(1,ICOL,IROW)
            ENDDO
      END SELECT

   ENDIF
! Now apply final scoring 
   IF (USE_3BY3_SCORING) THEN
      I = NINT(OUTPUTCOMP(1)%RZT(1,ICOL,IROW))
      J = NINT(OUTPUTCOMP(2)%RZT(1,ICOL,IROW))
      K = NINT(OUTPUTCOMP(3)%RZT(1,ICOL,IROW))
      OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = THREE_BY_THREE_SCORING(I,J,K)
   ELSE
      IF (TRIM(DISCRETE_OR_CONTINUOUS) .EQ. 'DISCRETE' .AND. NUMBER_OF_CATEGORIES_FOR_FINAL_HAZARD_SCALE .GT. 1) THEN
         VAL = OUTPUTCOMP(0)%RZT(1,ICOL,IROW)
         OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = CATEGORY_SCORES_FOR_FINAL_HAZARD_SCALE(1)
         DO ITHRESH = 1, NUMBER_OF_CATEGORIES_FOR_FINAL_HAZARD_SCALE - 1
            ICAT = ITHRESH + 1
            IF (VAL .GT. CATEGORY_THRESHOLDS_FOR_FINAL_HAZARD_SCALE(ITHRESH)) OUTPUTCOMP(0)%RZT(1,ICOL,IROW) = CATEGORY_SCORES_FOR_FINAL_HAZARD_SCALE(ICAT)
         ENDDO
      ENDIF
   ENDIF

ENDDO !ICOL
ENDDO !IROW

! Now write newly-calculated tile to disk:
 CALL WRITE_BIL_RASTER(OUTPUTCOMP(0), OUTPUT_DIRECTORY, OUTPUT_FILENAME_OF_FINAL_HAZARD_SCALE, .TRUE., .TRUE.)

!Write color table to tif image:
DO ICOMP = 0, NUMBER_OF_COMPONENTS
   IF (TRIM(COLOR_TABLE_FOR_COMPONENT(ICOMP)) .NE. 'null') THEN
      SHELLSTR='gdaldem color-relief ' // TRIM(FNOUTPUTCOMP(ICOMP)) // '.tif ' // TRIM(COLOR_TABLE_FOR_COMPONENT(ICOMP)) // ' ' // &
      TRIM(FNOUTPUTCOMP(ICOMP)) // '_rgb.tif ' // ' -co "COMPRESS=DEFLATE" -co "ZLEVEL=9"'
      WRITE(*,200) TRIM(SHELLSTR); CALL SYSTEM(TRIM(SHELLSTR))
   ENDIF
ENDDO

200 FORMAT(A)
500 FORMAT(A, 4(I6, ' '), 4(A))

! *****************************************************************************
END PROGRAM MAP_GENERATOR
! *****************************************************************************
