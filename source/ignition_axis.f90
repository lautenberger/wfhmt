! *****************************************************************************
PROGRAM CALC_IGNITION_AXIS
! *****************************************************************************
! This program calculates several possibilities for an "ignition axis" and 
! related input quantities, including: 
!  1. Mean temperature
!  2. Mean relative humidity
!  3. Mean wind speed
!  4. Mean equilibrium moisture content
!  5. Mean Schroeder ember ignition probability
!  6. Mean wind force (pounds per sq ft)

!  7. Mean Schroeder * mean wind force
!  8. Mean Schroeder * mean wind speed
!  9. Percentage of windspeed >= certain threshold 
! 10. Schroeder * percentage of windspeed > certain threshold

USE VARS
USE IO
USE SUBS

IMPLICIT NONE

INTEGER :: IBAND, IOS, IROW, ICOL, IRASTER, NBANDS
REAL :: MO30, ETA, F1, F2, PROBIGN, RASTERMINVAL(2), RASTERMAXVAL(2)
CHARACTER(400) :: FN, NAMELIST_FN
TYPE(RASTER_TYPE) :: RASTER

NAMELIST /IGNITION_AXIS_INPUTS/ INPUT_DIRECTORY, OUTPUT_DIRECTORY, A_SRS, WS_IN_METERS_PER_SECOND, &
                                TMP_IN_CELSIUS, NBANDS_FOR_IGNITION_AXIS, WS_THRESHOLD, ADD_TO_MEQ, NORMALIZE_PIGN, & 
                                RASTERMINVAL, RASTERMAXVAL, PATH_TO_GDAL, SCRATCH, &
                                WS_FILENAME, RH_FILENAME, TMP_FILENAME, FFWI_FILENAME, & !Input rasters
                                TMP_AVG_FILENAME, RH_AVG_FILENAME, WS_AVG_FILENAME, MEQ_AVG_FILENAME, FFWI_AVG_FILENAME, & !Output rasters
                                PIGN_FILENAME, PIGN_AVG_FILENAME, & !Output
                                LBF_FILENAME, LBF_AVG_FILENAME, & !Output
                                PIGN_LBF_FILENAME, PIGN_LBF_AVG_FILENAME, & !Output
                                PIGN_WS_FILENAME, PIGN_WS_AVG_FILENAME, & !Output
                                PCT_WS_ABOVE_THRESHOLD_FILENAME, PIGN_PCT_WS_ABOVE_THRESHOLD_FILENAME !Output

!Begin by getting input file name:
CALL GETARG(1,NAMELIST_FN)
IF (NAMELIST_FN(1:1)==' ') THEN
   WRITE(*,*) "Error, no input file specified."
   WRITE(*,*) "Hit Enter to continue."
   READ(5,*)
   STOP
ENDIF

!Now read NAMELIST group:
WRITE(*,*) 'Reading &IGNITION_AXIS_INPUTS namelist group from ', TRIM(NAMELIST_FN)

! Set defaults:
CALL SET_NAMELIST_DEFAULTS
RASTERMINVAL(:) = 9E9; RASTERMAXVAL(:) = -9E9

! Open input file and read in namelist group
OPEN(LUINPUT,FILE=TRIM(NAMELIST_FN),FORM='FORMATTED',STATUS='OLD',IOSTAT=IOS)
IF (IOS .GT. 0) THEN
   WRITE(*,*) 'Problem opening input file ', TRIM(NAMELIST_FN)
   STOP
ENDIF

READ(LUINPUT,NML=IGNITION_AXIS_INPUTS,END=100,IOSTAT=IOS)
 100  IF (IOS > 0) THEN
         WRITE(*,*) 'Error: Problem with namelist group &IGNITION_AXIS_INPUTS.'
         STOP
      ENDIF
CLOSE(LUINPUT)

!Get operating system (linux or windows/dos)
CALL GET_OPERATING_SYSTEM

! Get coordinate system string
FN=TRIM(INPUT_DIRECTORY) // TRIM(WS_FILENAME)
CALL GET_COORDINATE_SYSTEM(FN)

! Read input rasters:
CALL READ_BSQ_RASTER(WS   , INPUT_DIRECTORY, WS_FILENAME   )
CALL READ_BSQ_RASTER(RH   , INPUT_DIRECTORY, RH_FILENAME   )
CALL READ_BSQ_RASTER(TMP  , INPUT_DIRECTORY, TMP_FILENAME  )
CALL READ_BSQ_RASTER(FFWI , INPUT_DIRECTORY, FFWI_FILENAME )

! Adjust units to mph and deg F, if appropriate:
IF (WS_IN_METERS_PER_SECOND) WS%RZT (:,:,:) = WS%RZT (:,:,:) / 0.447
IF (TMP_IN_CELSIUS         ) TMP%RZT(:,:,:) = TMP%RZT(:,:,:) * 9./5. + 32.

! Set number of bands to use for ignition axis:
NBANDS = MIN(RH%NBANDS, NBANDS_FOR_IGNITION_AXIS)

! Allocate ignition probability and ignition axis rasters: 
CALL ALLOCATE_EMPTY_RASTER(MEQ           ,RH%NCOLS,RH%NROWS,NBANDS,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(LBF           ,RH%NCOLS,RH%NROWS,NBANDS,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(PIGN          ,RH%NCOLS,RH%NROWS,NBANDS,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(PIGN_LBF      ,RH%NCOLS,RH%NROWS,NBANDS,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(PIGN_WS       ,RH%NCOLS,RH%NROWS,NBANDS,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(RASTER        ,RH%NCOLS,RH%NROWS,NBANDS,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')

CALL ALLOCATE_EMPTY_RASTER(WS_AVG          ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(TMP_AVG         ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(RH_AVG          ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(MEQ_AVG         ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(LBF_AVG         ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(PIGN_AVG        ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(PIGN_LBF_AVG    ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(PIGN_WS_AVG     ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(FFWI_AVG        ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')

CALL ALLOCATE_EMPTY_RASTER(PCT_WS_ABOVE_THRESHOLD      ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
CALL ALLOCATE_EMPTY_RASTER(PIGN_PCT_WS_ABOVE_THRESHOLD ,RH%NCOLS,RH%NROWS,1     ,RH%XLLCORNER,RH%YLLCORNER,RH%XDIM,RH%YDIM,RH%NODATA_VALUE,1,'FLOAT     ')
PCT_WS_ABOVE_THRESHOLD%RZT     (:,:,:) = 0.
PIGN_PCT_WS_ABOVE_THRESHOLD%RZT(:,:,:) = 0.

! Now calculate MEQ, PIGN, and IGNITION_AXIS
DO IBAND = 1, NBANDS
   DO IROW = 1, RH%NROWS
   DO ICOL = 1, RH%NCOLS
      IF (ABS( TMP%RZT(IBAND,ICOL,IROW) -  TMP%NODATA_VALUE) .GT. 0.1 .AND. &
          ABS(  RH%RZT(IBAND,ICOL,IROW) -   RH%NODATA_VALUE) .GT. 0.1 .AND. &
          ABS(  WS%RZT(IBAND,ICOL,IROW) -   WS%NODATA_VALUE) .GT. 0.1) THEN
         CALL GET_MEQ(TMP%RZT(IBAND,ICOL,IROW),RH%RZT(IBAND,ICOL,IROW),ADD_TO_MEQ,MEQ%RZT(IBAND,ICOL,IROW))

! Calculate ignition probability and related quantities:
         CALL GET_PIGN(TMP%RZT(IBAND,ICOL,IROW),MEQ%RZT(IBAND,ICOL,IROW),PIGN%RZT(IBAND,ICOL,IROW))
         LBF%RZT(IBAND,ICOL,IROW) = 0.00256 * WS%RZT(IBAND,ICOL,IROW) * WS%RZT(IBAND,ICOL,IROW)
         PIGN_LBF%RZT(IBAND,ICOL,IROW) = PIGN%RZT(IBAND,ICOL,IROW) * LBF%RZT(IBAND,ICOL,IROW) 
         PIGN_WS%RZT(IBAND,ICOL,IROW) = PIGN%RZT(IBAND,ICOL,IROW) * WS%RZT(IBAND,ICOL,IROW)
         IF (WS%RZT(IBAND,ICOL,IROW) .GT. WS_THRESHOLD) THEN
            PCT_WS_ABOVE_THRESHOLD%RZT(1,ICOL,IROW) = PCT_WS_ABOVE_THRESHOLD%RZT(1,ICOL,IROW) + 1./REAL(NBANDS)
            PIGN_PCT_WS_ABOVE_THRESHOLD%RZT(1,ICOL,IROW) = PIGN_PCT_WS_ABOVE_THRESHOLD%RZT(1,ICOL,IROW) + PIGN%RZT(IBAND,ICOL,IROW) * 1./REAL(NBANDS) 
         ENDIF
      ELSE
         PIGN%RZT         (IBAND,ICOL,IROW) = PIGN%NODATA_VALUE
         LBF%RZT          (IBAND,ICOL,IROW) = LBF%NODATA_VALUE
         PIGN_LBF%RZT     (IBAND,ICOL,IROW) = PIGN_LBF%NODATA_VALUE
         PIGN_WS%RZT    (IBAND,ICOL,IROW) = PIGN_WS%NODATA_VALUE
         PCT_WS_ABOVE_THRESHOLD%RZT(1,ICOL,IROW) = PCT_WS_ABOVE_THRESHOLD%NODATA_VALUE
      ENDIF
   ENDDO !ICOL
   ENDDO !IROW
ENDDO !IBAND

WHERE (PIGN%RZT                       (:,:,:) .GT. 0. ) PIGN%RZT                       (:,:,:) = 100. * PIGN%RZT                       (:,:,:) 
WHERE (PCT_WS_ABOVE_THRESHOLD%RZT     (1,:,:) .GT. 0. ) PCT_WS_ABOVE_THRESHOLD%RZT     (1,:,:) = 100. * PCT_WS_ABOVE_THRESHOLD%RZT     (1,:,:) 
WHERE (PIGN_PCT_WS_ABOVE_THRESHOLD%RZT(1,:,:) .GT. 0. ) PIGN_PCT_WS_ABOVE_THRESHOLD%RZT(1,:,:) = 100. * PIGN_PCT_WS_ABOVE_THRESHOLD%RZT(1,:,:) 

! Now calculate average ignition-related rasters
WS_AVG%RZT           (1,:,:) = 0.
TMP_AVG%RZT          (1,:,:) = 0.
RH_AVG%RZT           (1,:,:) = 0.
MEQ_AVG%RZT          (1,:,:) = 0.
LBF_AVG%RZT          (1,:,:) = 0.
PIGN_AVG%RZT         (1,:,:) = 0.
PIGN_LBF_AVG%RZT     (1,:,:) = 0.
PIGN_WS_AVG%RZT      (1,:,:) = 0.
FFWI_AVG%RZT         (1,:,:) = 0.

DO IBAND = 1, NBANDS
   WS_AVG%RZT           (1,:,:) = WS_AVG%RZT       (1,:,:) + WS%RZT       (IBAND,:,:) / REAL(NBANDS)
   TMP_AVG%RZT          (1,:,:) = TMP_AVG%RZT      (1,:,:) + TMP%RZT      (IBAND,:,:) / REAL(NBANDS)
   RH_AVG%RZT           (1,:,:) = RH_AVG%RZT       (1,:,:) + RH%RZT       (IBAND,:,:) / REAL(NBANDS)
   MEQ_AVG%RZT          (1,:,:) = MEQ_AVG%RZT      (1,:,:) + MEQ%RZT      (IBAND,:,:) / REAL(NBANDS)
   LBF_AVG%RZT          (1,:,:) = LBF_AVG%RZT      (1,:,:) + LBF%RZT      (IBAND,:,:) / REAL(NBANDS)
   PIGN_AVG%RZT         (1,:,:) = PIGN_AVG%RZT     (1,:,:) + PIGN%RZT     (IBAND,:,:) / REAL(NBANDS)
   PIGN_LBF_AVG%RZT     (1,:,:) = PIGN_LBF_AVG%RZT (1,:,:) + PIGN_LBF%RZT (IBAND,:,:) / REAL(NBANDS)
   PIGN_WS_AVG%RZT      (1,:,:) = PIGN_WS_AVG%RZT  (1,:,:) + PIGN_WS%RZT  (IBAND,:,:) / REAL(NBANDS)
   FFWI_AVG%RZT         (1,:,:) = FFWI_AVG%RZT     (1,:,:) + FFWI%RZT     (IBAND,:,:) / REAL(NBANDS)
ENDDO

IF (NORMALIZE_PIGN) THEN
   DO IRASTER = 1, 2
      IF (IRASTER .EQ. 1) RASTER = PIGN_AVG
      IF (IRASTER .EQ. 2) RASTER = PIGN_PCT_WS_ABOVE_THRESHOLD

      IF (RASTERMINVAL(IRASTER) .GT. 8.9E9 .AND. RASTERMAXVAL(IRASTER) .LT. -8.9E9) THEN
         DO IROW = 1, RASTER%NROWS
         DO ICOL = 1, RASTER%NCOLS
            IF ( ABS( RASTER%RZT(1,ICOL,IROW) - RASTER%NODATA_VALUE ) .GT. 0.01 ) THEN
               IF (RASTER%RZT(1,ICOL,IROW) .GT. RASTERMAXVAL(IRASTER)) RASTERMAXVAL(IRASTER) = RASTER%RZT(1,ICOL,IROW)
               IF (RASTER%RZT(1,ICOL,IROW) .LT. RASTERMINVAL(IRASTER)) RASTERMINVAL(IRASTER) = RASTER%RZT(1,ICOL,IROW)
            ENDIF
         ENDDO
         ENDDO
      ENDIF

      WRITE(*,*) 'RASTERMINVAL: ', RASTERMINVAL(IRASTER)
      WRITE(*,*) 'RASTERMAXVAL: ', RASTERMAXVAL(IRASTER)

      IF (IRASTER .EQ. 1) PIGN_AVG%RZT                   (1,:,:) = 100.0 * (RASTER%RZT(1,:,:) - RASTERMINVAL(IRASTER)) / (RASTERMAXVAL(IRASTER) - RASTERMINVAL(IRASTER))
      IF (IRASTER .EQ. 2) PIGN_PCT_WS_ABOVE_THRESHOLD%RZT(1,:,:) = 100.0 * (RASTER%RZT(1,:,:) - RASTERMINVAL(IRASTER)) / (RASTERMAXVAL(IRASTER) - RASTERMINVAL(IRASTER))

   ENDDO
ENDIF

! Now write rasters to disk:
CALL WRITE_BIL_RASTER(WS_AVG         , OUTPUT_DIRECTORY, WS_AVG_FILENAME          ,.TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(TMP_AVG        , OUTPUT_DIRECTORY, TMP_AVG_FILENAME         ,.TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(RH_AVG         , OUTPUT_DIRECTORY, RH_AVG_FILENAME          ,.TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(MEQ_AVG        , OUTPUT_DIRECTORY, MEQ_AVG_FILENAME         ,.TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(FFWI_AVG       , OUTPUT_DIRECTORY, FFWI_AVG_FILENAME        ,.TRUE., .TRUE. )

CALL WRITE_BIL_RASTER(PIGN             , OUTPUT_DIRECTORY, PIGN_FILENAME              ,.TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(PIGN_AVG         , OUTPUT_DIRECTORY, PIGN_AVG_FILENAME          ,.TRUE., .TRUE. )

CALL WRITE_BIL_RASTER(LBF              , OUTPUT_DIRECTORY, LBF_FILENAME               ,.TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(LBF_AVG          , OUTPUT_DIRECTORY, LBF_AVG_FILENAME           ,.TRUE., .TRUE. )

CALL WRITE_BIL_RASTER(PIGN_LBF         , OUTPUT_DIRECTORY, PIGN_LBF_FILENAME          ,.TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(PIGN_LBF_AVG     , OUTPUT_DIRECTORY, PIGN_LBF_AVG_FILENAME      ,.TRUE., .TRUE. )

CALL WRITE_BIL_RASTER(PIGN_WS        , OUTPUT_DIRECTORY, PIGN_WS_FILENAME         ,.TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(PIGN_WS_AVG    , OUTPUT_DIRECTORY, PIGN_WS_AVG_FILENAME     ,.TRUE., .TRUE. )

CALL WRITE_BIL_RASTER(PCT_WS_ABOVE_THRESHOLD     , OUTPUT_DIRECTORY, PCT_WS_ABOVE_THRESHOLD_FILENAME      , .TRUE., .TRUE. )
CALL WRITE_BIL_RASTER(PIGN_PCT_WS_ABOVE_THRESHOLD, OUTPUT_DIRECTORY, PIGN_PCT_WS_ABOVE_THRESHOLD_FILENAME , .TRUE., .TRUE. )

! *****************************************************************************
END PROGRAM CALC_IGNITION_AXIS
! *****************************************************************************
